
mod std::derive

use macro::*

pub fun clone(input: List[Token], ctx: Compiler) -> List[Token] {
    val mem_clone: Fun(Node, String) -> String = |mem_t, mem_v| {
        match mem_t {
            Node::NamedType(Triplet(_, mtp, _)) {
                return "_::clone(_)" 
                    |> fmt(mtp |> values() |> join("::"), mem_v)
            }
            _ { return "(0)" }
        }
    }
    match input |> parse_decl(ctx) {

        Some(Node::StructDecl(StructDecl(_, path, type_args, members))) {
            val p: String = path |> values() |> join("::")
            val ta: String = type_args |> values() |> join(", ")
            val mems: String = members
                |> values()
                |> map[Pair[String, Node], String](
                    |mem| mem_clone(mem.second, "self." |> concat(mem.first))
                )
                |> join(", ")
            val clone_impl = "pub fun _::clone[_](self: _[_]) -> mut _[_] = _[_](_)"
                |> fmt(p, ta, p, ta, p, ta, p, ta, mems)
            println(clone_impl)
            val clone_impl_t: List[Token] = clone_impl 
                |> tokenize(ctx)
                |> expect("hard-coded declaration - should be valid")
            return input 
                |> concat(clone_impl_t)
        }
        
        Some(Node::EnumDecl(EnumDecl(_, path, type_args, members))) {
            val p: String = path |> values() |> join("::")
            val ta: String = type_args |> values() |> join(", ")
            val mems: String = members
                |> values()
                |> map[Pair[String, Node], String](|mem| {
                    val s = StringBuilder::empty()
                    s |> push(mem.first)
                    s |> push("(v) { return ")
                    s |> push(p)
                    s |> push("::")
                    s |> push(mem.first)
                    s |> push("[")
                    s |> push(ta)
                    s |> push("](")
                    s |> push(mem_clone(mem.second, "v"))
                    s |> push(") }")
                    return s |> as_string()
                })
                |> join("\n")
            val clone_impl = "pub fun _::clone[_](self: _[_]) -> mut _[_] {"
                |> concat("match self {")
                |> concat("_")
                |> concat("}")
                |> concat("}")
                |> fmt(p, ta, p, ta, p, ta, mems)
            println(clone_impl)
            val clone_impl_t: List[Token] = clone_impl 
                |> tokenize(ctx)
                |> expect("hard-coded declaration - should be valid")
            return input 
                |> concat(clone_impl_t)
        }
        
        _ { return input }
    }
}